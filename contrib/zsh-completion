#compdef netctl

local -a _long_opts _profile_opts
_long_opts=(
    '--help[Show help message]'
    '--version[Show version]'
    )

_profile_opts=(
    'start' 'stop' 'restart'
    'switch-to' 'status' 'enable'
    'disable' 'reenable'
    )

for fun in start switch-to; do
    (( $+function[_netctl_$fun] )) || _netctl_$fun(){
        local -a _profiles
        _profiles=( ${${${(f)"$(_call_program profiles "$service list 2>/dev/null")"}:#\**}##  })
        _describe 'Stopped Profiles' _profiles
    }
done

(( $+functions[_netctl_stop] )) || _netctl_stop(){
    local -a _profiles
    _profiles=( ${${(M)${(f)"$(_call_program profiles "$service list 2>/dev/null")"}:#\**}##\* })
    _describe 'Running Profiles' _profiles
}

(( $+functions[_netctl_restart] )) || _netctl_restart(){
    local -a _profiles
    _profiles=( ${${(f)"$(_call_program profiles "$service list 2>/dev/null")"}##[\* ] })
    _describe 'profiles' _profiles
}

(( $+functions[_netctl_enable] )) || _netctl_enable(){
    local -a _profiles
    _profiles=(${${(M)${(f)"$(_call_program "Disabled Netctl Units" "systemctl list-unit-files 2>/dev/null")"}:#netctl*disabled*}%% *}) 
    _describe "Disabled Netctl Units" _profiles
}

(( $+functions[_netctl_disable] )) || _netctl_disable(){
    local -a _profiles
    _profiles=(${${(M)${(f)"$(_call_program "Enabled Netctl Units" "systemctl list-unit-files 2>/dev/null")"}:#netctl*enabled*}%% *}) 
    _describe 'Enabled Netctl Units' _profiles
}

(( $+function[_netctl_reenable] )) || _netctl_reenable(){
    local -a _profiles
    _profiles=(${${(M)${(f)"$(_call_program "Netctl Units" "systemctl list-unit-files 2>/dev/null")"}:#netctl*}%% *}) 
    _describe "Netctl Units" _profiles
}

for fun in $_profile_opts[@]; do
    (( $+functions[_netctl_$fun] )) || _netctl_$fun () {
        local -a prof
        prof=( $(find -L /etc/network.d -maxdepth 1 -type f -not -name '.*' -not -name '*~' -not -name '*.conf' -not -name '*.service' -printf "%f\n") )
        compadd "$prof[@]"
    }
done

_netctl_command(){
    local -a _command_opts
    _command_opts=(
        'list:List available profiles'
        'store:Save which profiles are active'
        'restore:Load saved profiles'
        'stop-all:Stops all profiles'
        'start:Start a profile'
        'stop:Stop a profile'
        'restart:Restart a profile'
        'switch-to:Switch to a profile'
        'status:Show runtime status of a profile'
        'enable:Enable the systemd unit for a profile'
        'disable:Disable the systemd unit for a profile'
        'reenable:Reenable the systemd unit for a profile'
        )

    if (( CURRENT == 1 )); then
        _describe "Netctl Commands" _command_opts
    else
        cmd="${${_command_opts[(r)$words[1]:*]%%:*}}"
        if (( $#cmd )); then
            curcontext="${curcontext%:*:*}:loginctl-${cmd}:"
            _call_function ret _netctl_$cmd || _message "no more options"
        else
            _message "Unknown netctl command: $words[1]"
        fi
        return ret;
    fi
}

_arguments \
    "$_long_opts[@]" \
    '*::netctl commands:_netctl_command'
